package yal.analyse ;

import java.util.*;
import yal.arbre.* ;
import yal.arbre.expressions.* ;
import yal.arbre.expressions.binaire.arithmetique.* ;
import yal.arbre.expressions.binaire.comparaison.* ;
import yal.arbre.expressions.binaire.logique.* ;
import yal.arbre.expressions.unaire.* ;
import yal.arbre.instructions.* ;
import yal.exceptions.AnalyseSyntaxiqueException;
import yal.analyse.TDS ;
import yal.analyse.entree.* ;
import yal.analyse.symbole.* ;

import java_cup.runtime.*;

action code {:
             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;
    
        lesTerminaux.put(new Integer(CodesLexicaux.DEBUT), "debut") ;
        lesTerminaux.put(new Integer(CodesLexicaux.FIN), "fin") ;
        lesTerminaux.put(new Integer(CodesLexicaux.POINTVIRGULE), ";") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {                
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)                    
                    m.append(" colonne : " + (s.right+1)) ;
            }
            
            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CSTENTIERE, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non termin√©e") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  */

terminal Symbol    PROGRAMME, DEBUT, FIN, POINTVIRGULE, ECRIRE, EGAL, LIRE ;
terminal Symbol    TANTQUE, REPETER, FINTANTQUE, SI, ALORS, SINON, FINSI ;
terminal Symbol    PLUS, MOINS, MULTIPLICATION, DIVISION, PAROUVR, PARFER ;
terminal Symbol    EGALITE, INFERIEUR, SUPERIEUR, DIFFERENCE, ET, OU, NON ;
terminal String    IDF, CSTENTIERE, TYPE ;

   
/* Non terminals used in the grammar section.  */

non terminal Symbol LDECL, DECL, DECL_VAR ;
non terminal ArbreAbstrait PROG ;
non terminal BlocDInstructions LINST ;
non terminal Expression EXP, OPER ;
non terminal Instruction INST, AFFECTATION, ECRITURE, LECTURE, BOUCLE, CONDITION ;


/* Precedences */
precedence left OU;
precedence left ET;
precedence left EGALITE, DIFFERENCE;
precedence left INFERIEUR, SUPERIEUR;
precedence left PLUS, MOINS;
precedence left MULTIPLICATION, DIVISION;
precedence left NON;

/* ----------------------------Grammar Section-------------------- */
 
start with PROG ;

PROG    ::=     PROGRAMME IDF DEBUT LDECL LINST:li FIN
                {: RESULT = li ; :}
        |
                PROGRAMME IDF DEBUT LINST:li FIN
                {: RESULT = li ; :}
                ;


LINST   ::=     LINST:li  INST:i
                {: li.ajouter(i) ;
                   RESULT = li ; :}
        |
                INST:i
                {: BlocDInstructions li = new BlocDInstructions(ileft + 1) ;
                   li.ajouter(i) ;
                   RESULT = li ; :}
                ;


INST    ::=     AFFECTATION:a
                {: RESULT = a ; :}
        |
                BOUCLE:b
                {: RESULT = b ; :}
        |
                CONDITION:c
                {: RESULT = c ; :}
        |
                LECTURE:l
                {: RESULT = l ; :}
        |
                ECRITURE:e
                {: RESULT = e ; :}
                ;


LDECL   ::=     LDECL DECL
                {: :}
        |
                DECL
                {: :}
                ;

DECL    ::=     DECL_VAR
                {: :}
                ;


DECL_VAR  ::=   TYPE:t IDF:i POINTVIRGULE
                {: EntreeVariable e = new EntreeVariable(i);
                   SymboleVariable s = new SymboleVariable("entier");
                   TDS.getInstance().ajouter(e, s, tleft+1); :}
                ;


AFFECTATION ::= IDF:i EGAL EXP:e POINTVIRGULE
                {: RESULT = new Affectation(i, e) ; :}
                ;


BOUCLE    ::=   TANTQUE EXP:e REPETER LINST:li FINTANTQUE
                {: RESULT = new Boucle(e,li) ; :}
                ;


CONDITION ::=   SI EXP:e ALORS FINSI
                {: RESULT = new Condition(e) ; :}
          |
                SI EXP:e ALORS LINST:li FINSI
                {: RESULT = new Condition(e,li,0) ; :}
          |
                SI EXP:e ALORS LINST:la SINON LINST:ls FINSI
                {: RESULT = new Condition(e,la,ls) ; :}
          |
                SI EXP:e ALORS SINON LINST:li FINSI
                {: RESULT = new Condition(e,li,1) ; :}
                ;


LECTURE ::= LIRE IDF:i POINTVIRGULE
                {: RESULT = new Lire(i, ileft + 1 ); :}
                ;


ECRITURE ::=    ECRIRE EXP:e POINTVIRGULE
                {: RESULT = new Ecrire(e, eleft + 1) ; :}
                ;

    
EXP    ::=      CSTENTIERE:c
                {: RESULT = new ConstanteEntiere(c, cleft + 1) ; :}
        |
                IDF:i
                {: RESULT = new Variable(i, ileft + 1) ; :}
        |
                PAROUVR EXP:e PARFER
                {: RESULT = e; :}
        |
                NON EXP:e
                {: RESULT = new Non(e) ; :}
        |
                MOINS EXP:e
                {: RESULT = new MoinsUnaire(e) ; :}
        |
                EXP:eg PLUS EXP:ed
                {: RESULT = new Plus(eg, ed) ; :}
        |
                EXP:eg MOINS EXP:ed
                {: RESULT = new Moins(eg, ed) ; :}
        |
                EXP:eg MULTIPLICATION EXP:ed
                {: RESULT = new Multiplier(eg, ed) ; :}
        |
                EXP:eg SUPERIEUR EXP:ed
                {: RESULT = new Superieur(eg, ed) ; :}
        |
                EXP:eg INFERIEUR EXP:ed
                {: RESULT = new Inferieur(eg, ed) ; :}
        |
                EXP:eg DIVISION EXP:ed
                {: RESULT = new Diviser(eg, ed) ; :}
        |
                EXP:eg EGALITE EXP:ed
                {: RESULT = new Egalite(eg, ed) ; :}
        |
                EXP:eg DIFFERENCE EXP:ed
                {: RESULT = new Different(eg, ed) ; :}
        |
                EXP:eg ET EXP:ed
                {: RESULT = new Ou(eg, ed) ; :}
        |
                EXP:eg OU EXP:ed
                {: RESULT = new Et(eg, ed) ; :}
                ;

